X:stylesheet {

X:output method="html" version="4.0" indent="no" encoding="utf-8";

X:strip-space elements = "*";

<!--
  -- a current directory of a XSLT script is where the script is stored,
  -- but not where XSLT processor has been started to run the script
  -->
X:param XML = "'../xml'";
X:param YEAR;

X:var LINK = "/article/@link";

X:include href = "dirname.xslt";
X:include href = "link.xslt";
X:include href = "style.xslt";
X:include href = "body.xslt";
X:include href = "menu.xslt";
X:include href = "ga.xslt";
X:include href = "content.xslt";


X:template = "/article" {
    <html><head>

    <title> !{@name} </title>

    !style (lang="@lang")

    !ga ()

    </head>

    !body (lang="@lang")

    </html>
}


X:template = "download" { <table width="100%"> !! "item"; </table> }


X:template = "download/item[position() &lt;= ../@last]" {
    <tr>
        <td width="20%">
        X:if "position() = 1" {
            <a>
            X:attribute "href" { X:text{/} !{/article/@lang} X:text{/} !{../@changes} }
            !{../@changes}
            </a>
        }
        </td>

        <td width="20%">
        <a>
        X:attribute "href" {
            X:text {/download/nginx-} !{@ver} X:text {.tar.gz}
        }
        X:text {nginx-} !{@ver}
        </a>

        X:if "@pgp = 'yes'" {
            X:text {&#xA0; }
            <a>
            X:attribute "href" {
                X:text {/download/nginx-} !{@ver} X:text {.tar.gz.asc}
            }
            X:text {pgp}
            </a>
        }
        </td>

        <td>
        X:if "@win= 'yes'" {
            <a>
            X:attribute "href" {
                X:text {/download/nginx-} !{@ver} X:text {.zip}
            }
            X:text {nginx/Windows-} !{@ver}
            </a>

            X:if "@pgp = 'yes'" {
                X:text {&#xA0; }
                <a>
                X:attribute "href" {
                    X:text {/download/nginx-} !{@ver} X:text {.zip.asc}
                }
                X:text {pgp}
                </a>
            }
        }
        </td>

    </tr>
}


X:template = "download/item[position() &gt; ../@last]" { }

}
